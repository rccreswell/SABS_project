[[model]]
author: Chon Lok Lei
name: lei_2019_ikr
desc: """
Myokit version of Beattie et al. 2018 22C IKr model.

"""
# Initial values
ikr.open = 0
ikr.active = 1

[ikr]
use membrane.V
IKr = g * open * active * (V - rev.EK)
dot(open) = (inf - open) / tau
    inf = k1 * tau
    tau = 1 / (k1 + k2)
    k1 = p1 * exp(p2 * V)
    k2 = p3 * exp(-p4 * V)
dot(active) = (inf - active) / tau
    inf = k4 * tau
    tau = 1 / (k3 + k4)
    k3 = p5 * exp(p6 * V)
    k4 = p7 * exp(-p8 * V)
p1 = 2.26e-4 [1/ms]
p2 = 0.0699 [1/mV]
p3 = 3.45e-5 [1/ms]
p4 = 0.05462 [1/mV]
p5 = 0.0873 [1/ms]
p6 = 8.91e-3 [1/mV]
p7 = 5.15e-3 [1/ms]
p8 = 0.03158 [1/mV]
g = 0.1524 * 1e3 [pA/mV]  # in nA range

[engine]
time = 0 bind time
    in [ms]
pace = 0 bind pace

[membrane]
V = engine.pace
    desc: membane potential
    in [mV]

#
# Potassium concentrations
#
[potassium]
Ki = 110 [mM]
Ko = 4 [mM]

#
# Sodium concentrations
#
[sodium]
Nai = 10 [mM]
Nao = 97.5 [mM]

#
# Calcium concentrations (clamp)
#
[calcium]
Cai = 1e-5 in [mM]
Cao = 2.05 [mM]


#
# Reversal parameters
#
[rev]
use phys.RTF
ECa = 0.5 * RTF * log(calcium.Cao / calcium.Cai)
    in [V]
EK = RTF * log(potassium.Ko / potassium.Ki)
    in [V]
ENa = RTF * log(sodium.Nao / sodium.Nai)
    in [V]


#
# Physical constants
#
[phys]
R = 8.314472 [J/mol/K]
T = 295 [K]  # 37C (BT)
F = 9.64853415e4 [C/mol]
RTF = R * T / F * 1e3 in [mV]

